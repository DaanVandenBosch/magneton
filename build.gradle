plugins {
    id 'kotlin-multiplatform' version '1.3.30'
    id 'org.jetbrains.kotlin.frontend' version '0.0.45'
    id 'maven-publish'
}

group = 'magneton'
version = '0.0.1-SNAPSHOT'

repositories {
    jcenter()
}

wrapper {
    gradleVersion = "5.3.1"
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }
    js {
        compilations.main {
            kotlinOptions {
                outputFile = "$project.buildDir.path/classes/kotlin/js/main/${project.name}.js"
            }
        }
        compilations.test {
            kotlinOptions {
                outputFile = "$project.buildDir.path/classes/kotlin/js/test/${project.name}.js"
            }
        }
        compilations.all {
            kotlinOptions {
                metaInfo = true
                languageVersion = '1.3'
                moduleKind = 'umd'
                sourceMap = true
                sourceMapEmbedSources = 'always'
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                // Kotlin
                implementation kotlin('stdlib-jdk8')
                implementation kotlin('reflect')

                // Logging
                implementation "org.slf4j:slf4j-api:1.7.25"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                // Kotlin
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

kotlinFrontend {
    sourceMaps = true

    npm {
        dependency "text-encoding", "0.7.0"
        devDependency "karma"
    }

    webpackBundle {
        bundleName = "main"
        sourceMapEnabled = true
        contentPath = file("src/jsMain/web")
        mode = "development"
        port = 3300
    }

    karma {
        port = 3303
        runnerPort = 3304
        reporters = ['progress']
        frameworks = ['qunit']
    }

    define("PRODUCTION", false)
}
